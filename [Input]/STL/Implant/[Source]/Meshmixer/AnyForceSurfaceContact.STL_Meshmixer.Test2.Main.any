Main = 
{
  AnyFolder Model = 
  {
    AnyFixedRefFrame GlobalRef = 
    {
      AnyDrawRefFrame Drw = 
      {
        RGB = {0, 1, 0};
        
        ScaleXYZ = 0.015*{1, 1, 1};
      };
      
      AnyRefNode Node_Joint_Femur = 
      {
        sRel = {0.015, 0.044 - 0.002, 0};
      };
            
      AnyRefNode Node_Joint_Patella = 
      {
        sRel = {-0.025, 0.05, 0};       
      };

    };//GlobalRef
    
    AnyFolder Segs = 
    {      
      AnySeg Tibial = 
      {
        Mass = 0;
        Jii = {0, 0, 0};
        AnySurfSTL ContactSurf_Medial = 
        {
          //FileName = "Tibial Insert_Medial_Meshmixer.stl";
          FileName = "Tibial Insert_Medial_Cut_Ver2_Meshmixer.stl";
          AnyDrawSurf DrwSurf = 
          {
            FileName = .FileName;
            ScaleXYZ = .ScaleXYZ;
            RGB = 0.5*{1, 1, 1};
            Opacity = 0.5;
          };          
        };
        AnySurfSTL ContactSurf_Lateral = 
        {
          //FileName = "Tibial Insert_Lateral_Meshmixer.stl";
          FileName = "Tibial Insert_Lateral_Cut_Ver2_Meshmixer.stl";
          AnyDrawSurf DrwSurf = 
          {
            FileName = .FileName;
            ScaleXYZ = .ScaleXYZ;
            RGB = 0.5*{1, 1, 1};
            Opacity = 0.5;
          };          
        };            
      };
      AnySeg Femoral = 
      {
        Mass = 0;
        Jii = {0, 0, 0};   
        r0 = ..GlobalRef.Node_Joint_Femur.sRel;
        AnySurfSTL ContactSurf = 
        {
          //FileName = "Femoral Component_Meshmixer.stl";
          FileName = "Femoral Component_Cut_Ver2_Meshmixer.stl";
          AnyDrawSurf DrwSurf = 
          {
            FileName = .FileName;
            ScaleXYZ = .ScaleXYZ;
            RGB = 0.5*{1, 1, 1};
            Opacity = 0.5;
          };              
        };
      };
      AnySeg Patella = 
      {
        Mass = 0;
        Jii = {0, 0, 0};
        r0 = ..GlobalRef.Node_Joint_Patella.sRel;
        AnySurfSTL ContactSurf = 
        {
          FileName = "Patellar Button_Cut_Ver2_Meshmixer.stl";
          AnyDrawSurf DrwSurf = 
          {
            FileName = .FileName;
            ScaleXYZ = .ScaleXYZ;
            RGB = 0.5*{1, 1, 1};
            Opacity = 0.5;
          };         
        };         
      };      
      
    };//Segs
    
    AnyFolder Joints = 
    {
      AnyStdJoint Fix_Tibia = 
      {
        AnyRefFrame& ref0 = ..GlobalRef ;
        AnyRefFrame& ref1 = ..Segs.Tibial ;
      };
      
      AnyRevoluteJoint Rev_Femoral = 
      {
        AnyRefFrame& ref0 = ..GlobalRef.Node_Joint_Femur ;
        AnyRefFrame& ref1 = ..Segs.Femoral;
        Axis = z ;
      };
      
      AnyStdJoint Fix_Patella = 
      {
        AnyRefFrame& ref0 = ..GlobalRef.Node_Joint_Patella ;
        AnyRefFrame& ref1 = ..Segs.Patella ;
      };
    };//Joints
    
    AnyFolder Drivers = 
    {
      AnyKinEqSimpleDriver Rev_Femoral_Rotation =
      {
        AnyKinMeasure& ref0 = ..Joints.Rev_Femoral ;
        DriverPos = pi/180*{-10};
        DriverVel = pi/180*{-90};
      };
    };//Drivers
    
    AnyFolder Contacts = 
    {
      AnyForceSurfaceContact Femur_Tibia_Lateral = 
      {
        AnySurface& Master = ..Segs.Femoral.ContactSurf;
        AnySurface& Slave = ..Segs.Tibial.ContactSurf_Lateral ;
        
        PressureModule = 4.68e+009;
        ForceViewOnOff = On;
        MeshRefinementMaster = 0;
        MeshRefinementSlave = 0;
        SingleSidedOnOff= 0;
        
        ConnectorNormalDotTresholdMaster = 0.8;
        ConnectorNormalDotTresholdSlave= 0.8;
        
        AnyDrawForceSurface DrwSurfaceContact = 
        {
          DrawSurf1RGB = {1, 0, 0};
          DrawSurf2RGB = {0, 0, 1};
        };       
      };
      
      AnyForceSurfaceContact Femur_Tibia_Medial = 
      {
        AnySurface& Master = ..Segs.Femoral.ContactSurf;
        AnySurface& Slave = ..Segs.Tibial.ContactSurf_Medial ;
        
        PressureModule = 4.68e+009;
        ForceViewOnOff = On;
        MeshRefinementMaster = 0;
        MeshRefinementSlave = 0;
        SingleSidedOnOff= 0;
        
        ConnectorNormalDotTresholdMaster = 0.8;
        ConnectorNormalDotTresholdSlave= 0.8;
        
        AnyDrawForceSurface DrwSurfaceContact = 
        {
          DrawSurf1RGB = {1, 0, 0};
          DrawSurf2RGB = {0, 0, 1};
        };       
      };     
      
      AnyForceSurfaceContact Femur_Patella = 
      {
        AnySurface& Master = ..Segs.Femoral.ContactSurf;
        AnySurface& Slave = ..Segs.Patella.ContactSurf ;
        
        PressureModule = 4.68e+009;
        ForceViewOnOff = On;
        MeshRefinementMaster = 0;
        MeshRefinementSlave = 0;
        SingleSidedOnOff= 0;
        AnyDrawForceSurface DrwSurfaceContact = 
        {
          DrawSurf1RGB = {1, 0, 0};
          DrawSurf2RGB = {0, 0, 1};
        };       
      };       
    };//Contacts
  };//Model
  
  AnyBodyStudy Study = 
  {
    AnyFolder& Model = .Model;
    
    Gravity = {0, -9.81, 9};
    tStart = 0;
    tEnd = 1;
    nStep = 100;
    
  };
};
