Main =
{
  Studies = 
  {
    #if USE_FDK == 1    
    
    AnyBodyStudy InitStudy = 
    {
      AnyFolder& Parameters = ..SubjectSpecificData.Ligaments ;
      
      AnyFolder& Thigh   = .HumanModel.BodyModel.Right.Leg.Seg.Thigh;
      AnyFolder& Shank   = .HumanModel.BodyModel.Right.Leg.Seg.Shank;
      AnyFolder& Patella = .HumanModel.BodyModel.Right.Leg.Seg.Patella;
      AnyFolder& PatellaFemur = .HumanModel.BodyModel.Right.Leg.Jnt.PatellaFemur;
      AnyFolder& KinematicMeasures = .HumanModel.BodyModel.Right.Leg.Jnt.KinematicMeasures;
      AnyFolder& PatellaMovement = .HumanModel.BodyModel.Right.Leg.Jnt.PatellaMovement;
      AnyFolder& Ligaments = .HumanModel.BodyModel.Right.Leg.Ligaments;
      
      AnyFolder Drivers_Init = 
      {
        AnyKinEqSimpleDriver Thigh_Right_Lin =
        {
          AnyKinLinear lin = 
          {
            AnyRefFrame& ref0 = REF_HUMAN_BODYMODEL.Right.Leg.Seg.Thigh;
            
          };//lin
          DriverPos = REF_HUMAN_BODYMODEL.Right.Leg.Seg.Thigh.r0 ;
          DriverVel = {0, 0, 0};
          Reaction.Type = {Off, Off, Off};
        };//Thigh_Right_Lin
        AnyKinEqSimpleDriver Thigh_Right_Rot =
        {
          AnyKinRotational rot = 
          {
            AnyRefFrame& ref0 = REF_HUMAN_BODYMODEL.Right.Leg.Seg.Thigh.KneeJoint;            
            Type = RotAxesAngles;
            Axis1 = z;
            Axis2 = y;
            Axis3 = x;             
          };//rot
          DriverPos = pi/180*
          {
            Main.TrialSpecificData.InitialPositionOfBody.PelvisRotZ, 
            Main.TrialSpecificData.InitialPositionOfBody.PelvisRotY, 
            Main.TrialSpecificData.InitialPositionOfBody.PelvisRotX
          };
          DriverVel = pi/180*{0, 0, 0};
          Reaction.Type = {Off, Off, Off};
        };//Thigh_Right_Lin        
        
        AnyKinEqSimpleDriver KneeJoint_Right = 
        {
          AnyKinLinear lin = 
          {
            AnyRefFrame & ref0 = REF_HUMAN_BODYMODEL.Right.Leg.Seg.Thigh.KneeJoint;
            AnyRefFrame & ref1 = REF_HUMAN_BODYMODEL.Right.Leg.Seg.Shank.KneeJoint;
            Ref = 0;
          };
          AnyKinRotational rot = 
          {
            AnyRefFrame & ref0 = REF_HUMAN_BODYMODEL.Right.Leg.Seg.Thigh.KneeJoint;
            AnyRefFrame & ref1 = REF_HUMAN_BODYMODEL.Right.Leg.Seg.Shank.KneeJoint;
            Type = PlanarAngles;
          };
          DriverPos = {0, -DEF_CUSTOM_KNEE_JOINT_CONTACT_INIT_OFFSET, 0, 0, 0, 0};
          DriverVel = {0, 0, 0, 0, 0, 0};
          Reaction.Type = {Off, Off, Off, Off, Off, Off};
        }; //KneeJoint_Right 
      };      
      
      Gravity = Main.TrialSpecificData.Gravity;
      tStart = 0.0 ;
      tEnd = 1.0 ;
      nStep = 1 ;
      AnyFolder OutputFunctions_Length = {};    
    };//InitStudy
    
    AnyDesEvalStudy EvalStudy = 
    {
      Analysis =
      {
        AnyOperation& RunInit = ..InitStudy.Kinematics;
      }; 
    };//EvalStudy    
    
    AnyOperationSequence LigamentCalibrationSequence = 
    {
      AnyOperationMacro LoadParametersOptimizationResults = 
      {
        MacroStr = 
        ({
          "classoperation Main.Studies.LoadParametersOptimizationResults" + " " + strquote("Load design") + " --file=" + strformat("\x22") + "../Input/" + OutputFileNamePrefix + Main.ModelSetup.C3DFileData.NameOfFile +"-"+"OptimizedParameters.txt" + strformat("\x22"), 
          "classoperation Main" + strformat("\x22")+ "Update Values" + strformat("\x22")        
        });
      };
      AnyOperation& EvaluateLigamentLengths = .EvalStudy.UpdateDesign;
      AnyOperationMacro UpdateValues = 
      {
        MacroStr = 
        {
          "classoperation Main" + strquote("Update Values")
        };
      };  
    };//LigamentCalibrationSequence   
    
    InverseDynamicStudy =
    {
      #if (BM_LEG_MUSCLES_RIGHT != OFF) & (BM_LEG_MUSCLES_LEFT != OFF)
      AnyObjectPtr pArrExcludedMuscles = arrcat
      (
        Main.Studies.HumanModel.BodyModel.Right.Leg.Mus.ExcludedMuscles.Objects,
        Main.Studies.HumanModel.BodyModel.Left.Leg.Mus.ExcludedMuscles.Objects
      );  
      AnyMechObjectExcluder ExcluderSelectiveMuscles = 
      {
        Objects = .pArrExcludedMuscles;
      };
      #endif 
      
      #ifdef USE_FDK_TIBIOFEMORAL
      REF_HUMAN_BODYMODEL.Right.Leg.Jnt.Knee = 
      {
        Constraints.CType  = {ForceDep, ForceDep, ForceDep, ForceDep, ForceDep};
        Constraints.Reaction.Type = {Off, Off, Off, Off, Off};
      };
      #endif  
      
      #ifdef USE_FDK_PATELLOFEMORAL
      REF_HUMAN_BODYMODEL.Right.Leg.Jnt.PatellaFemur =
      {
        Constraints.CType  = {ForceDep, ForceDep, ForceDep, ForceDep, ForceDep};
        Constraints.Reaction.Type = {Off, Off, Off, Off, Off};    
      };
      #endif      
      
      InverseDynamics.ForceDepKinOnOff=On;
      InverseDynamics.ForceDepKin.SolverType = FDK_SolverType ;
      InverseDynamics.ForceDepKin.ForceTol = FDK_ForceTol ;
      InverseDynamics.ForceDepKin.UseAdaptiveForceTolOnOff = FDK_UseAdaptiveForceTolOnOff ;
      InverseDynamics.ForceDepKin.Perturbation = FDK_Perturbation;
      InverseDynamics.ForceDepKin.MaxIteration = FDK_MaxIteration;
      InverseDynamics.ForceDepKin.MaxNewtonStep = FDK_MaxNewtonStep;       
      InverseDynamics.ForceDepKin.LocalSearchOnOff = FDK_LocalSearchOnOff;     
    };
    #endif
    
  };//Studies
  
  #if USE_FDK == 1
  AnyOperationSequence InverseDynamicAnalysisSequence =
  {
    AnyOperation& LigamentCalibrationSequence = .Studies.LigamentCalibrationSequence;  
    AnyOperation& InverseDynamicStudy = .Studies.InverseDynamicStudy.InverseDynamics;
    
    #ifdef AutoSaveOption 
    AnyOperationMacro SaveResult = 
    {
      MacroStr=
      { 
        "classoperation Main.Studies.InverseDynamicStudy.Output" + strquote("Save data") + " --file="+ "../Output/" + OutputFileNamePrefix + Main.ModelSetup.C3DFileData.NameOfFile + ".anydata.h5 " + "--type=Deep"
      };
    };
    #endif      
  };    
  #else
  AnyOperationMacro InverseDynamicAnalysisSequence =
  {
    MacroStr =
    ({
      "classoperation Main.Studies.LoadParametersOptimizationResults" + " " + strquote("Load design") + " --file=" + strformat("\x22") + "../Input/" + OutputFileNamePrefix + Main.ModelSetup.C3DFileData.NameOfFile +"-"+"OptimizedParameters.txt" + strformat("\x22"), 
      "classoperation Main" + strformat("\x22")+ "Update Values" + strformat("\x22"),
      "operation Main.Studies.InverseDynamicStudy.InitialConditions",
      "run",
      "operation Main.Studies.HumanModel.Calibration.CalibrationSequence",
      "run",
      "operation Main.Studies.InverseDynamicStudy.InverseDynamics",
      "run"
      #if AutoSaveOption
      ,
      "classoperation Main.Studies.InverseDynamicStudy.Output" + strquote("Save data") + " --file="+ "../Output/" + OutputFileNamePrefix + Main.ModelSetup.C3DFileData.NameOfFile + ".anydata.h5 " + "--type=Deep"
      #endif      
    });
  };
  #endif  
  
};
