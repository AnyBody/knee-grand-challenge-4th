AnyKinMeasureFunComb1 FunComb = 
{
  AnyKinMeasure& ref = .TargetMeasure;
  Functions = {&F1};
  
  AnyFunInterpol F1 = 
  {
    AnyVar RangeMin = ..RangeMin;
    AnyVar LowerLimit = ..LowerLimit;
    AnyVar RangeMax = ..RangeMax;
    AnyVar HighLimit = ..HighLimit;
    
    AnyVar CoeffFactor = 500.0;
    AnyInt PowerIndex = 4;
    
    AnyIntVar T_left_steps = 100;
    AnyVar T_left_delta = (LowerLimit - RangeMin)/T_left_steps;
    AnyVector T_left_base = farr(RangeMin, T_left_delta, T_left_steps+1);
    AnyVector T_left = T_left_base ;
    AnyVector T_left_offset = T_left_base - LowerLimit;
    AnyVector Data_left = pow(-1, PowerIndex)*CoeffFactor*pow(T_left_offset, PowerIndex) ;
    
    AnyIntVar T_mid_steps = 100;
    AnyVar T_mid_delta = (HighLimit - LowerLimit)/T_mid_steps;
    AnyVector T_mid = arrcat(
    arrcat( 
    (LowerLimit + T_mid_delta) - {3/4*T_mid_delta, 2/4*T_mid_delta, 1/4*T_mid_delta},
    farr(LowerLimit + T_mid_delta , T_mid_delta, T_mid_steps-1)
    ),
    (HighLimit - T_mid_delta) + {1/4*T_mid_delta, 2/4*T_mid_delta, 3/4*T_mid_delta}
    );
    AnyVector Data_mid = arrcat(arrcat({0.0, 0.0, 0.0}, 0.0*farr(0.0, 1.0, T_mid_steps-1)), {0.0, 0.0, 0.0});
    
    AnyIntVar T_right_steps = 100;
    AnyVar T_right_delta = (RangeMax - HighLimit)/T_right_steps;
    AnyVector T_right_base = farr(HighLimit, T_right_delta, T_right_steps+1);  
    AnyVector T_right = T_right_base;
    AnyVector T_right_offset = T_right_base - HighLimit;
    AnyVector Data_right = pow(1, PowerIndex)*CoeffFactor*pow(T_right_offset, PowerIndex);
    
    AnyVector T_total = arrcat(arrcat(T_left, T_mid), T_right);
    AnyVector Data_total = arrcat(arrcat(Data_left, Data_mid), Data_right);
    
    Type = Bspline;
    T = T_total ;
    Data = {Data_total} ;
  };
};

AnyKinEqSimpleDriver JointLimitDriver = 
{
  AnyKinMeasureFunComb1 & ref = .FunComb;
  DriverPos = {0};
  DriverVel = {0};
  Reaction.Type = {Off};
  CType = {Soft};
  AnyFunConst ConstFun = { Value = {100.0}; };
  WeightFun = {&ConstFun};      
};
